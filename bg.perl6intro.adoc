= Увод в Perl 6
Naoum Hankache <naoum@hankache.com>; Красимир Беров <berov@cpan.org>
:description: Общо въведение в Perl 6
:keywords: perl6, perl 6, въведение, perl6intro, въведение в perl 6, урок по пърл 6, увод в perl 6
:Revision: 1.0
:icons: font
:source-highlighter: pygments
//:pygments-style: manni
:source-language: perl6
:pygments-linenums-mode: table
:toc: left
:toc-title: Съдържание
:doctype: book
:lang: bg


Предназначението на това писание е да ви въведе набързо в езика за програмиране Perl 6. 
То ще ви даде начален тласък в използването и изучаването му. 

В някои от разделите ще намерите препратки към други, по-пълни и точни части от
http://docs.perl6.org[документацията на Perl 6].  Така ще можете да четете
направо от източника, в случай че се нуждаете от повече информация по
определена тема.

В този урок ше намерите примери по най-обсъжданите теми.
За да ги разберете по-добре, отделяйте време да правите примерите.

.Лиценз
Тази работа е лицензирана под "Признание - Споделяне на Споделеното 4.0 Международен Лиценз".
За да видите лиценза, посетете

* https://creativecommons.org/licenses/by-sa/4.0/.

.Принос
Ако желаете да допринесте за подобряването на това писание, отидете на

* https://github.com/hankache/perl6intro

.Отзиви
Всякакви отзиви са добре дошли:

* naoum@hankache.com

* berov@cpan.org

Ако тази работа ви харесва, отбележете хранилището със звезда на следния адрес:
link:https://github.com/hankache/perl6intro[Github].

.Преводи

* Български: http://bg.perl6intro.com
* Испански: http://es.perl6intro.com
* Немски: http://de.perl6intro.com
* Португалски: http://pt.perl6intro.com
* Френски: http://fr.perl6intro.com
* Японски: http://ja.perl6intro.com

:sectnums:
== Увод
=== Какво е Perl 6
Perl 6 е език от високо ниво, с общо предназначение и постепенна типизация на променливите.
Perl 6 е многопарадигмен. Той поддържа процедурно, обектно-ориентирано и функционално програмиране.

.Девизи на Perl 6:
* ИННН Има няколко начина да се направи. TMTOWTDI (Произнася се Tim Toady):
There is more than one way to do it.
* Лесните неща трябва да са лесни, трудните да стават по-лесни, а невъзможните - трудни.

=== Понятия
* *Perl 6*: Това е спецификация за език за програмиране с набор от тестове.
Реализации, които изпълняват тестовете без грешка, могат да се нарекат Perl 6.
* *Rakudo*: Е компилатор за Пърл 6.
* *Rakudobrew*: Е програма за управление инсталациите на Ракудо.
* *Panda*: Е инсталатор на модули за Пърл 6.
* *Rakudo Star*: Е вързоп: Ракудо, Панда, набор от модули за Пърл 6 и документация.

=== Инсталиране на Пърл 6
.Линукс
. Инсталирайте Ракудобрю: https://github.com/tadzik/rakudobrew

. Инсталирайте Ракудо: Изпълнете следната команда в терминал `rakudobrew build moar`

. Инсталирайте Панда: Изпълнете следната команда в терминал `rakudobrew build panda`

. Инсталирайте Task::Star. Това е мета-пакет, съдържащ модулите, които вървят с
изданието Rakudo Star. Изпълнете следната команда в терминал `panda install Task::Star`

За да видите други възможности за инсталация, посетете
http://rakudo.org/how-to-get-rakudo/#Installing-Rakudo-Star-Linux

.OSX
Имате четири възможности:

* Следвайте същите стъпки като в Линукс
* Инсталирайте с хоумбрю: `brew install rakudo-star`
* Инсталирайте с МакПортс: `sudo port install rakudo`
* Свалете най-новият инсталатор (файл с разширение .dmg) от http://rakudo.org/downloads/star/

.Windows
. Свалете най-новия инсталатор (файл с разширение .msi) от
http://rakudo.org/downloads/star/ Ако архитектурата на системата ви е
32-битова, свалете файла с x86 в името; ако е 64-битова, свалете файла съдържащ
x86_64 в името.
. След инсталацията се уверете, че `C:\rakudo\bin` се намира в системната
променлива PATH

.Docker
. Вземете официалното изображение за Docker `docker pull rakudo-star`
. След това стартирайте контейнер с изображението `docker run -it rakudo-star`

=== Изпълнение на код, написан на Perl 6

Можете да изпълнявате код на Пърл 6, като използвате директно неговата
интерактивна конзола - REPL (Read-Eval-Print Loop). За да направите това,
отворете терминал, напишете `perl6` в терминала и натиснете [Enter]. Това ще
отвори конзолата и в нея ще се появи `>`.  След това напишете някакъв програмен
код и натиснете [Enter]. На следващия ред в конзолата ще се появи резултатът от
изпълнението на кода.  Въведете друг ред, съдържащ програмен код, или въведете
`exit` и натиснете [Enter], за да напуснете конзолата (REPL).

Друг начин за изпълнение е, като въведете програмния код във файл, запишете го и
го изпълните. Препоръчва се за разширение на скриптовете, написани на Пърл 6, да
се използва `.pl6`. Изпълнете файла, като напишете в терминал `perl6
filename.pl6` и натиснете  [Enter]. За разлика от интерактивната конзола
(REPL), всеки ред код ще се изпълни последователно, но резултатът не ще се
изпише на екрана автоматично. Кодът трябва да съдържа израз, използващ командата
`say`, за да изведе нещо на стандартния изход (екрана).

Интерактивната конзола се използва най-вече за пробване на специфични парченца
код, обикновено едноредови изрази. За програми, състоящи се от повече редове, се
препоръчва да се записват във файл и след това да се изпълняват.

Едноредови изрази могат да се изпробват и на командния ред без интерактиванта
конзола, като напишете `perl6 -e 'your code here'` и натиснете [Enter].

[TIP]
--
Rakudo Star върви с едноредов редактор, който се използва в интерактивната конзола (REPL).

Ако сте инсталирали обикновен Rakudo, вместо Rakudo Star, най-вероятно
интерактивната конзола не ви дава възможност да редактирате текущия ред, да
ползвате стрелка нагоре и надолу (за да извиквате предишни команди и
да ги променяте) или да ползвате табулация (клавишът TAB) за допълване на
частично въведени низове. Изпълнете една от следните команда и сте готови.

* `panda install Linenoise` ще работи в Windows, Linux и OSX

* `panda install Readline` - ако сте на Линукс и предпочитате библиотеката _Readline_
--

=== Редактори
Тъй като през повечето време ще записваме програмите си във файлове, ни е нужен
приличен текстов редактор, който разпознава синтаксиса на Пърл 6.

Аз лично използвам и препоръчвам https://atom.io/[Atom]. Това е модерен редактор
и поддържа синтаксиса на Пърл 6.
https://atom.io/packages/language-perl6fe[Perl6-fe] е допълнителен пакет за
оцветяване на кода на Пърл 6 за Атом. Той произхожда от оригиналния пакет,
който идва с Атом, но съдържа много подобрения и поправени грешки.

Други членове на общността използват също http://www.vim.org/[Vim], https://www.gnu.org/software/emacs/[Emacs] или http://padre.perlide.org/[Padre].

По-новите версии на Vim идват по подразбиране с поддръжка на синтаксиса на Пърл
6. Emacs и Padre изискват инсталиране на допълнителни пакети.


=== Здравей, Свят!
Ще започнем с ритуала `hello world`.

[source,perl6]
say 'Здравей, Свят!';

Това може да бъде написано и така:

[source,perl6]
'Здравей, Свят!'.say;

=== Преглед на синтаксиса
Пърл 6 е *свободна форма*: Свободни сте (през повечето време) да използвате
колкото ви е угодно празни пространства (за разлика от Питон - бел. прев.).

*Твърденията* са обикновено логически ред код. Те завършват с точка и запетая. +
`say "Здрасти" if True;`

*Изразите* са специален тип твърдение, което връща стойност:
`1+2` ще върне `3`

Изразите се състоят от *Членове* и *Оператори*.

*Членове*. Те са:

* *Променливи*: Съдържат стойност, която може да бъде променяна.

* *Буквални стойности (литерали)*: Непроменяема, буквална стойност - число или низ.

*Оператори*. Те са няколко типа:

|===

| *Тип* | *Обяснение* | *Пример*

| Представка | Преди члена. | `++1`

| Вставка | Между членовете | `1+2`

| Наставка | След члена | `1++`

| Ограждащ | Около члена | `(1)`

| Ограждаща наставка | След един член и ограждащ друг член | `Array[1]`

|===

==== Идентификатори (Имена)
Идентификаторите представляват имена, дадени на членовете.

.Правила:
* Трябва да започват с буква или знак за подчертавка.

* Могат да съдържат числа (ако не са първия знак в името на променливата).

* Могат да съдържат тирета или апострофи (ако не са първи или последен знак).
От дясната страна на тирето или апострофа винаги трябва да има буква.

|===

| *Правилно* | *Неправилно*

| `var1` | `1var`

| `var-one` | `var-1`

| `var'one` | `var'1`

| `var1_` | `var1'`

| `_var` | `-var`

|===

.Честo използвани начини за именуване (конвенции):
* КамилоОбразно: `variableNo1`

* шиш-кебап: `variable-no1`

* змие_видно: `variable_no1`

Можете да именувате променливите си както искате, но е добра практика да се
спрете на един вариант и да го следвате.

Като използвате смислени имена, ще улесните живота на всички - и вашият, и на вашите колеги.

* `var1 = var2 * var3` е правилно синтактично, но безсмислено.
* `monthly-salary = daily-rate * working-days` тези са по-смислени имена за променливи.

==== Коментари
Коментарът е текст, който се пропуска от компилатора, и се ползва като бележка или пояснение.

Коментарите са три типа:

* Едноредови:
+
[source,perl6]
# Това е едноредов коментар

* Вложен/вмъкнат:
+
[source,perl6]
say #`(Това е вмъкнат коментар) "Hello World."

* Многоредови:
+
[source,perl6]
-----------------------------
=begin comment
Това е многоредов коментар.
Първа бележка
Второ пояснение
=end comment
-----------------------------

==== Кавички
Низовете се ограждат с двойни или единични кавички.

Използвайте двойни кавички, когато:

* низът ви съдържа апостроф;

* низът ви съдържа променливи.

[source,perl6]
-----------------------------------
say 'Hello World';   # Hello World
say "Hello World";   # Hello World
say "Don't";         # Don't
my $name = 'John Doe';
say 'Hello $name';   # Hello $name
say "Hello $name";   # Hello John Doe
-----------------------------------

== Оператори

=== Често използвани оператори
Следващата таблица изрежда най-често използваните оператори.
[cols="^.^5m,^.^5m,.^20,.^20m,.^20m", options="header"]
|===

| Оператор | Тип | Описание | Пример | Резултат

| + | Вставка | Събиране | 1 + 2 | 3

| - | Вставка | Изваждане | 3 - 1 | 2

| * | Вставка | Умножение | 3 * 2 | 6

| ** | Вставка | Степенуване | 3 ** 2 | 9

| / | Вставка | Деление | 3 / 2 | 1.5

| div | Вставка | Деление на цели числа (закръгля надолу) | 3 div 2 | 1

| % | Вставка | Деление до остатък | 7 % 4 | 3

.2+| %% .2+| Вставка .2+| Делимост | 6 %% 4 | False

<| 6 %% 3 <| True

| gcd | Вставка | Най-голям общ знаменател | 6 gcd 9 | 3

| lcm | Вставка | Най-малко общо кратно | 6 lcm 9 | 18

| == | Вставка | Цифрово равенство | 9 == 7  | False

| != | Вставка | Цифрово неравенство | 9 != 7  | True

| < | Вставка | По-малко | 9 < 7  | False

| > | Вставка | По-голямо | 9 > 7  | True

| \<= | Вставка | По-малко или равно | 7 \<= 7  | True

| >= | Вставка | По-голямо или равно | 9 >= 7  | True

| eq | Вставка | Еднаквост между низове | "John" eq "John"  | True

| ne | Вставка | Низовете не са еднакви | "John" ne "Jane"  | True

| = | Вставка | Присвояване | my $var = 7  | Присвояване на стойността `7` на променливата `$var`

.2+| ~ .2+| Вставка .2+| Свързване на низове | 9 ~ 7 | 97

<m| "Ей, " ~ "здрасти"  <| Ей, здрасти

.2+| x .2+| Вставка .2+| Повторение на низове | 13 x 3  | 131313

<| "Здрасти " x 3  <| Здрасти Здрасти Здрасти 

.5+| ~~ .5+| Вставка .5+| Умно съвпадение | 2 ~~ 2  | True

<| 2 ~~ Int <| True

<| "Perl 6" ~~ "Perl 6" <| True

<| "Perl 6" ~~ Str <| True

<| "enlightenment" ~~ /light/ <| ｢light｣

.2+| ++ | Представка | Увеличаване | my $var = 2; ++$var;  | Увеличава стойността на променливата с 1 и връща резулата `3`

<m| Наставка <d| Увеличаване <m| my $var = 2; $var++;  <| Връща стойността на променливата `2` и след това я увеличава с 1

.2+|\--| Представка | Намаляване | my $var = 2; --$var;  | Намалява стойността на променливата с 1 и връща резултата `1`

<m| Наставка <d| Намаляване <m| my $var = 2; $var--;  <| Връща стойността на променливата `2` и след това я намалява

.3+| + .3+| Представка .3+| Свежда операнда до числова стойност | +"3"  | 3

<| +True <| 1

<| +False <| 0

.3+| - .3+| Представка .3+| Свежда операнда до числова стойност и връща отрицанието | -"3"  | -3

<| -True <| -1

<| -False <| 0

.6+| ? .6+| Представка .6+| Свежда операнда до булева стойност | ?0 | False

<| ?9.8 <| True

<| ?"Hello" <| True

<| ?"" <| False

<| my $var; ?$var; <| False

<| my $var = 7; ?$var; <| True

| ! | Представка | Свежда операнда до булева стойност и връща отрицанието | !4 | False

| .. | Вставка | Създател на поредица |  0..5  | Създава поредица от 0 до 5

| ..^ | Вставка | Създател на поредица |  0..^5  | Създава поредица от 0 до 4

| ^.. | Вставка | Създател на поредица |  0^..5  | Създава поредица от 1 до 5

| \^..^ | Вставка |  Създател на поредица |  0\^..^5  | Създава поредица от 1 до 4

| ^ | Представка | Създател на поредица |  ^5  | Също като 0..^5 Създава поредица от 0 до 4

| ... | Вставка | Мързелив създател на списък |  0...9999  | връща елементите само при поискване

.2+| {vbar} .2+| Представка .2+| Сплескване | {vbar}(0..5)  | (0 1 2 3 4 5)

<| {vbar}(0\^..^5)  <| (1 2 3 4)

|===

=== Обърнати Оператори

Добавяне на `R` преди който и да е оператор ще обърне операндите му.

[cols=".^m,.^m,.^m,.^m", options="header"]
|===
| Нормална операция | Резултат | Обърнат Оператор | Резултат

| 2 / 3 | 0.666667 | 2 R/ 3 | 1.5

| 2 - 1 | 1 | 2 R- 1 | -1

|===

=== Съкращаващи Оператори

Операторите за съкращения се прилагат върху списък от стойности.
Съставят се, като операторът се огради със `[]`

[cols=".^m,.^m,.^m,.^m", options="header"]
|===
| Нормална операция | Резултат | Съкращаващ оператор | Резултат

| 1 + 2 + 3 + 4 + 5 | 15 | [+] 1,2,3,4,5 | 15

| 1 * 2 * 3 * 4 * 5 | 120 | [*] 1,2,3,4,5 | 120

|===

NOTE:  За да видите пълния списък с оператори и приоритетите им, отидете на
https://docs.perl6.org/language/operators

== Променливи
Променливите в Пърл 6 биват три типа - Скалари, Масиви и Хешове.

Променливите се различават по т.нар *сиджил* (означава "знак" на латински). Този
знак се намира в началото на всяка променлива.

* `$` се използва за скалари
* `@` се използва за масиви
* `%` се използва за хешове

=== Скалари 
Скаларът (Scalar) съдържа единична стойност или указател (reference).
[NOTE]
====
Бел. прев.:
Указателите в Пърл не са като указателите в C, и затова са познати
повече като референции. Все пак по-подходящата дума е указател, а не
референция.
====

[source,perl6]
----
#Низ (String)
my $name = 'Перко Наумов';
say $name;

#Цяло число (Integer)
my $age = 99;
say $age;
----

В зависимост от стойността, която съдържа скаларната променлива (нейния тип),
върху нея могат да се извършват различни действия.

[source,perl6]
.Низ
----
my $name = 'Перко Наумов';
say $name.uc;
say $name.chars;
say $name.flip;
----

----
ПЕРКО НАУМОВ
12
вомуаН окреП
----

NOTE: За да видите пълния списък с методите, приложими върху низове (скаларни
променливи от тип Str), вижте https://docs.perl6.org/type/Str

[source,perl6]
.Цяло число
----
my $age = 17;
say $age.is-prime;
----

----
True
----

NOTE: За да видите пълния списък с методите, приложими върху цели числа
(скаларни променливи от тип Int), вижте https://docs.perl6.org/type/Int

[source,perl6]
.Рационално число
----
my $age = 2.3;
say $age.numerator;
say $age.denominator;
say $age.nude;
----

----
23
10
(23 10)
----

NOTE: За да видите пълния списък с методите, приложими върху рационални числа
(десетични дроби), вижте https://docs.perl6.org/type/Rat

=== Масиви 
Масивите (Arrays) са списъци, които съдържат множество стойности.

[source,perl6]
----
my @animals = 'камила','лама','сова';
say @animals;
----

От долния пример се вижда, че върху масивите могат да се извършват много операции:

TIP: Тилдата `~` се използва за свързване на низове.

[source,perl6]
.`Script`
----
my @animals = 'camel','vicuña','llama';
say "В зоологическата градина има " ~ @animals.elems ~ " животни.";
say "Животните са: " ~ @animals;
say "Аз ще осиновя една сова";
@animals.push("owl");
say "Сега в градината ми има: " ~ @animals;
say "The first animal we adopted was the " ~ @animals[0];
@animals.pop;
say "Unfortunately the owl got away and we're left with: " ~ @animals;
say "We're closing the zoo and keeping one animal only";
say "We're going to let go: " ~ @animals.splice(1,2) ~ " and keep the " ~ @animals;
----

.`Изход`
----
В зоологическата градина има 3 животни.
Животните са: camel vicuña llama
Аз ще осиновя една сова
Сега в градината ми има: camel vicuña llama owl
The first animal we adopted was the camel
Unfortunately the owl got away and we're left with: camel vicuña llama
We're closing the zoo and keeping one animal only
We're going to let go: vicuña llama and keep the camel
----

.Обяснение
`.elems` връща броя на елементите в масива. +
`.push()` добавя един или повече елементи към масива. +
Можем да достъпим отделен елемент от масива като укажем мястото му `@animals[0]`. +
`.pop` премахва последния елемент от масива и го връща. +
`.splice(a,b)` ще премахне `b` елемента като започне от позиция `a`.

==== Масиви с предопределен размер
Обикновено масив се обявява по следния начин:
[source,perl6]
my @array;

По подразбиране масивът има неопределен брой елементи и затова го наричат
автоматично-разширяващ се.  Масивът ще приеме какъвто и да е брой стойности без
ограничение.

Но можем да създаваме и масиви с определен брой елементи. В такива масиви не
можете да достъпите или добавяте елементи извън определения размер на масива.

За да обявите масив с определен брой елементи, добавете броя на елементите в
квадратни скоби веднага след името на масива.
[source,perl6]
my @array[3];

Този масив ще може да съдържа най-много три стойности с места от 0 до 2.

[source,perl6]
----
my @array[3];
@array[0] = "първа стойност";
@array[1] = "втора стойност";
@array[2] = "трета стойност";
----

Не можете да добавите четвърта стойност в този масив:
[source,perl6]
----
my @array[3];
@array[0] = "първа стойност";
@array[1] = "втора стойност";
@array[2] = "трета стойност";
@array[3] = "четвърта стойност";
----

----
Index 3 for dimension 1 out of range (must be 0..2)
----

==== Многомерни масиви 
Масивите, които видяхме до тук, са едномерни. За щастие можем да създаваме и
многомерни масиви в Пърл 6.

[source,perl6]
my @tbl[3;2];

Този масив е двумерен.
Първото измерение може да съдържа най-много 3 стойности, а второто - най-много 2.

Представете си го като таблица с 3 реда и 2 колони.

[source,perl6]
----
my @tbl[3;2];
@tbl[0;0] = 1;
@tbl[0;1] = "x";
@tbl[1;0] = 2;
@tbl[1;1] = "y";
@tbl[2;0] = 3;
@tbl[2;1] = "z";
say @tbl
----

----
[[1 x] [2 y] [3 z]]
----

.Нагледно представяне на масива:
----
[1 x]
[2 y]
[3 z]
----

NOTE: За да видите пълния справочник за масив (Array), посетете
https://docs.perl6.org/type/Array

=== Хешове
[source,perl6]
.Хешът (Hash) е набор от двойки Ключ/Стойност.
----
my %столици = ('UK','London','Germany','Berlin');
say %столици;
----

[source,perl6]
.Ето и друг сбит начин за попълване на хеша:
----
my %capitals = (UK => 'London', Germany => 'Berlin');
say %capitals;
----

Някои от методите, които могат да се извикват върху хешове, са:
[source,perl6]
.`Скрипт`
----
my %capitals = (UK => 'London', Germany => 'Berlin');
%capitals.push: (France => 'Paris');
say %capitals.kv;
say %capitals.keys;
say %capitals.values;
say "The capital of France is: " ~ %capitals<France>;
----

.`Изход`
----
(France Paris Germany Berlin UK London)
(France Germany UK)
(Paris Berlin London)
The capital of France is: Paris
----

.Обяснение
`.push: (key => 'Value')` добавя нова двойка ключ/стойност. +
`.kv` връща списък, съдържаш всички ключове и стойности. +
`.keys` връща списък, съдържаш всички ключове. +
`.values` връща списък, съдържаш всички стойности. +
Можем да достъпим отделна стойност в хеша, като укажем нейния ключ `%hash<key>`

NOTE: За да видите пълния справочник за хешовете, посетете https://docs.perl6.org/type/Hash

=== Типове
В примерите досега не задавахме типа стойност, който да съдържа променливата.

TIP: `.WHAT` Ще върне типа на стойността, съдържаща се в променливата.

[source,perl6]
----
my $var = 'Text';
say $var;
say $var.WHAT;

$var = 123;
say $var;
say $var.WHAT;
----

Както виждате от горния пример, типа на стойността в променливата `$var` първо
беше (Str)  и след това (Int).

Този начин на програмиране се нарича динамично типизиране. Динамично означава,
че променливите могат да съдържат стойности от *Всякакъв* (Any) тип.

Сега опитайте да изпълните следния пример. Обърнете внимание на използването на
`Int` пред името на променливата.

[source,perl6]
----
my Int $var = 'Text';
say $var;
say $var.WHAT;
----

Присвояването ще се провали и ще върне следната грешка: +
 `Type check failed in assignment to $var; expected Int but got Str` +
 `Проверката за тип е неуспешна при присвояване на $var; очакваше се Int, но бе подаден Str`

Този път указахме, че типа на променливата ще бъде (Int).
Опитът да му присвоим низ (Str) не беше успешен.

Този начин на програмиране се нарича статично типизиране. Статично означава, че типа на променливите се указва предварително и не може да бъде променян.

Пърл 6 е *постепенно типизиран*; позволява *статично* и *динамично* типизиране.

.Масивите и хешовете могат също да бъдат статично типизирани:
[source,perl6]
----
my Int @array = 1,2,3;
say @array;
say @array.WHAT;

my Str @multilingual = "Здравей", "Hello","Salut","Hallo","您好","안녕하세요","こんにちは";
say @multilingual;
say @multilingual.WHAT;

my Str %capitals = (UK => 'London', Germany => 'Berlin');
say %capitals;
say %capitals.WHAT;

my Int %country-codes = (UK => 44, Germany => 49);
say %country-codes;
say %country-codes.WHAT;
----

.Ето списък с най-често използваните типове:
Най-вероятно никога няма да използвате първите два, но са упоменати, да ги знаете.

[cols="^.^1m,.^3m,.^2m,.^1m, options="header"]
|===

| *Тип* | *Описание* | *Пример* | *Резултат*

| Mu | Коренът на йерархията на типовете в Перл 6 | |

| Any | Подразбиращият се родителски клас за нови класове и за повечето стандартни класове | |

| Cool | Стойност, която може да бъде ползвана като низ и число едновременно | my Cool $var = 31; say $var.flip; say $var * 2; | 13 62

| Str | Низ от знакове | my Str $var = "NEON"; say $var.flip; | NOEN

| Int | Цяло число (случайна точност) | 7 + 7 | 14

| Rat | Рационално число (ограничена точност) | 0.1 + 0.2 | 0.3

| Bool | Булева стойност (Истина или Лъжа) | !True | False

|===

=== Самонаблюдение

Самонаблюдение (Introspection) е действието по взимане на информация за даден обект, например какъв е типът му. +
В един от предишните примери използвахме `.WHAT`, за да върнем типа на променливата.

[source,perl6]
----
my Int $var;
say $var.WHAT;    # (Int)
my $var2;
say $var2.WHAT;   # (Any)
$var2 = 1;
say $var2.WHAT;   # (Int)
$var2 = "Hello";
say $var2.WHAT;   # (Str)
$var2 = True;
say $var2.WHAT;   # (Bool)
$var2 = Nil;
say $var2.WHAT;   # (Any)
----

Типът на дадена променлива показва каква стойност може да съдържа. +
Типът на *силно* обявена празна променлива е типът, с който е била обявена. +
Типът на празна променлива, която не е силно обявена, е *Всякакъв* `(Any)` +
За да изчистите стойността на променлива, присвойте ѝ `Nil`.

=== Видимост
Преди да използвате променлива за първи път, трябва да я обявите.

В Пърл 6 се използват няколко начина за обявяване на променливи. В примерите
досега използвахме `my`.

[source,perl6]
my $var=1;

Операторът `my` дава на променливата *словна* (*lexical*) видимост.
Иначе казано, променливата ще бъде видима (използваема) само в блока от код, в който е обявена.

В Пърл 6 блокът представлява всичко, намиращо се между двойка отваряща и затваряща фигурни скоби - `{ }`.
Ако няма определен блок, променливата е достъпна в целия скрипт.

[source,perl6]
----
{
  my Str $var = 'Text';
  say $var; #is accessible
}
say $var; # е недостъпна, връща грешка
----

Тъй като променливата е видима само в блока, в който е обявена, можете да
ползвате същото име за друга променлива в друг блок.

[source,perl6]
----
{
  my Str $var = 'Text';
  say $var;
}
my Int $var = 123;
say $var;
----

=== Присвояване и Обвързване
В предишните примери видяхме как да *присвояваме* стойности на променливи. +
*Присвояването* се прави с помощта на оператора `=`.
[source,perl6]
----
my Int $var = 123;
say $var;
----

Можем да променим стойността, присвоена на променлива:

[source,perl6]
.Присвояване
----
my Int $var = 123;
say $var;
$var = 999;
say $var;
----

.`Изход`
----
123
999
----

И напротив - когато *обвързваме* стойност с променлива, не можем да променим стойността. +
*Обвързването* се извършва с помощта на оператора `:=`.

[source,perl6]
.Обвързване
----
my Int $var := 123;
say $var;
$var = 999;
say $var;
----

.`Изход`
----
123
Cannot assign to an immutable value
----

[source,perl6]
.Променливите могат да бъдат обвързвани и с други променливи:
----
my $a;
my $b;
$b := $a;
$a = 7;
say $b;
$b = 8;
say $a;
----

.`Изход`
----
7
8
----

Както забелязахте, обвързването на променливи една с друга е двупосочно. +
Резултатът от `$a := $b` и `$b := $a` е един и същ.

NOTE: Повече за променливите ще научите на адрес https://docs.perl6.org/language/variables

== Функции и мутатори

Важно е да се прави разлика между функции и мутатори. +
Функциите не променят първоначалното състояние на обектите, върху които са извикани. +
Мутаторите (менячи - бел. прев.) променят състоянието на обекта.

[source,perl6,linenums]
.`Скрипт`
----
my @числа = [7,2,4,9,11,3];

@числа.push(99);
say @числа;      #1

say @числа.sort; #2
say @числа;      #3

@числа.=sort;
say @числа;      #4
----

.`Изход`
----
[7 2 4 9 11 3 99] #1
(2 3 4 7 9 11 99) #2
[7 2 4 9 11 3 99] #3
[2 3 4 7 9 11 99] #4
----

.Обяснение
`.push` е меняч (мутатор), той променя състоянието на масива (#1)

`.sort` е функция, тя връща подреден масив, но не променя състоянието на масива, върху който се използва:

* (#2) показва, че връща подреден масив.

* (#3) показва, че първоначалното състояние на масива е непроменено.

За да накараме дадена функция да действа като мутатор, използваме `.=` вместо `.` (#4) (ред 9 от скрипта)

== Цикли и условия
Пърл 6 има много изрази за условия и цикли.

=== if
Кодът се изпълнява само ако условието е изпълнено, иначе казано, ако изразът се изчисли като Истина (`True`).

[source,perl6]
----
my $age = 19;

if $age > 18 {
  say 'Welcome'
}
----

В Пърл 6 можем да сменим местата на условието и кода. +
Макар местата да са сменени, проверката на условието винаги се изпълнява първа.

[source,perl6]
----
my $age = 19;

say 'Добре дошъл' if $age > 18;
----

В случай че условието не се изпълни, можем да укажем алтернативни блокове код чрез:

* `else`
* `elsif`

[source,perl6]
----
#изпълнение на различен код при различни стойности на променливата
my $брой-места = 9;

if $брой-места <= 5 {
  say 'Аз съм седан'
} elsif $брой-места  <= 7 {
  say 'Аз съм мини-ван'
} else {
  say 'Аз съм ван'
}
----

=== unless
Отрицанието на твърдението, проверявано чрез `if`, може да бъде изразено чрез `unless`.

Следният код:

[source,perl6]
----
my $чисти-обувки = False;

if not $чисти-обувки {
  say 'Почисти си обувките!'
}
----
може да бъде написано:

[source,perl6]
----
my $чисти-обувки = False;

unless $чисти-обувки {
  say 'Почисти си обувките!'
}
----

Отрицание на дадено твърдение се постига чрез `!` или `not`.

`unless (условие)` се използва вместо `if not (условие)`.

`unless` не може да има съответстваща `else` клауза.

=== with

`with` е като `if`, но проверява дали променливата има присвоена стойност.

[source,perl6]
----
my Int $var=1;

with $var {
  say 'Hello'
}
----

Ако изпълните кода, без да сте присвоили стойност на променливата, нищо няма да се изведе на екрана.
[source,perl6]
----
my Int $var;

with $var {
  say 'Hello'
}
----

`without` е обратното на `with`. Същото каквото е `unless` за `if`.

Ако първото `with` условие не е изпълнено, може да укажете друго условие с `orwith`. +
`with` и `orwith` са подобни на `if` и `elsif`.

=== for

Цикълът `for` повтаря действието върху множество стойности.

[source,perl6]
----
my @array = [1,2,3];

for @array -> $array-item {
  say $array-item * 100
}
----

Забележете, че създадохме променливата `$array-item`, за да приложим действието
`*100` върху всеки елемент от масива.

=== given

`given` в Пърл 6 е същото като `switch` в другите езици, но много по-мощно.

[source,perl6]
----
my $var = 42;

given $var {
    when 0..50 { say 'По-малко или равно на 50' }
    when Int { say "е Int" }
    when 42  { say 42 }
    default  { say "к'во?" }
}
----

При успешно съвпадение, процесът на търсене на удовлетворяване на условието,
следващо `when`, се прекратява.

Ако обаче добавите `proceed` в блока за изпълнение, процесът на търсене на
съвпадение продължава.
[source,perl6]
----
my $var = 42;

given $var {
    when 0..50 { say 'По-малко или равно на 50'; proceed }
    when Int { say "е Int"; proceed }
    when 42  { say 42 }
    default  { say "к'во?" }
}
----

=== loop

`loop` е друг начин за писане на `for` цикъл.

Всъщност `loop` е начинът, по който се пишат `for` циклите в езиците, подобни на C.

Пърл 6 принадлежи към това семейство.

[source,perl6]
----
loop (my $i = 0; $i < 5; $i++) {
  say "Текущото число е $i"
}
----

NOTE: За да научите повече за циклите и условните изрази, погледнете
https://docs.perl6.org/language/control

== I/O
В Пърл 6 най-често използваните _входно-изходни_ интерфейси са _терминалът_ и _файловете_.

=== Основни действия в терминал

==== say
`say` пише в стандартния изход. Добавя нов ред в края. С други думи, следният код

[source,perl6]
----
say 'Hello Mam.';
say 'Hello Sir.';
----
ще изведе текста в кавичките на два отделни реда.

==== print
`print` има подобно поведение като `say`, но не добавя нов ред.

Заместете `say` с `print` и сравнете изхода от двете действия.

==== get
`get` се използва за прихващане на входни данни от терминала.

[source,perl6]
----
my $name;

say "Hi, what's your name?";
$name = get;

say "Dear $name welcome to Perl 6";
----

При изпълнение на горния код, терминалът ще чака да въведете името си и да
натиснете [Enter].  След това ще ви поздрави.

==== prompt
`prompt` е съчетание от `print` и `get`.

Горният пример може да бъде написан така:

[source,perl6]
----
my $name = prompt "Hi, what's your name? ";

say "Dear $name welcome to Perl 6";
----

=== Изпълнение на външни команди
Две подпрограми могат да се използват за извикване на външни команди:

* `run` Изпълнява външна команда (програма) без посредничеството на системната обвивка.

* `shell` Изпълнява команда през системната обвивка. Тя е зависима от
операционната система и от обвивката ѝ. Всички мета-знаци на обвивката се
интерпретират от нея - включително `|`, пренасочването на променливите на
обкръжението и т.н.

[source,perl6]
.Изпълнете следното, ако сте в Linux/OSX
----
my $name = 'Neo';
run 'echo', "hello $name";
shell "ls";
----

[source,perl6]
.Изпълнете следното в Windows
----
shell "dir";
----
`echo` и `ls` са познати команди от обвивката в Linux: +
`echo` отпечатва текст в терминала (същото като `print` в Perl 6) +
`ls` показва списък от файлове и папки в текущата папка

`dir` е същото като `ls`, но в Windows.


=== Писане в и четене от файлове
==== slurp
`slurp` се използва за четене на данни от файл наведнъж.

Създайте файл със следното съдържание:

.datafile.txt
----
John 9
Johnnie 7
Jane 8
Joanna 7
----
[source,perl6]
----
my $data = slurp "datafile.txt";
say $data;
----

==== spurt
`spurt` се използва за запис на данни във файл наведнъж.

[source,perl6]
----
my $newdata = "New scores:
Paul 10
Paulie 9
Paulo 11";

spurt "newdatafile.txt", $newdata;
----

С изпълнението на горния код ще се създаде нов файл, именуван _newdatafile.txt_.
Той ще съдържа данните от `$newdata`.

=== Работа с файлове и папки
Perl 6 може да покаже списък от папки и файлове без помощта на системни команди като `ls`, както бе показано в един от предишните примери.

[source,perl6]
----
say dir;              #Показва списък със съдържанието на текущатата папка
say dir "/Documents"; #Показва списък със съдържанието на указаната папка
-
----

Освен това, можете да създавате нови папки и да ги триете.

[source,perl6]
----
mkdir "newfolder";
rmdir "newfolder";
----

`mkdir` създава нова папка. +
`rmdir` изтрива празна папка. Връща грешка, ако не е празна.

Също така можете да проверявате дали указаният път съществува, дали е файл или папка:

В папката, където ще изпълните долния скрипт, създайте празна папка `folder123` и празен файл с разширение pl6 `script123.pl6`

[source,perl6]
----
say "script123.pl6".IO.e;
say "folder123".IO.e;

say "script123.pl6".IO.d;
say "folder123".IO.d;

say "script123.pl6".IO.f;
say "folder123".IO.f;
----

`IO.e` проверява дали файлът съществува. +
`IO.f` проверява дали указаният път е файл. +
`IO.d` проверява дали указаният път е папка.

WARNING: Потребителите на Windows могат да използуват `/` или `\\` за разделител +
`C:\\rakudo\\bin` +
`C:/rakudo/bin` +

NOTE: За повече информация, свързана с входно-изходните операции, вижте https://docs.perl6.org/type/IO

== Подпрограми
=== Създаване
*Подпрограмите* (наричани също *функции*) са начин да се събере накуп набор от
действия (функционалност). +

За да създадете подпрограма, напишете ключовата дума `sub`, последвана от името
на подпрограмата. След това подпрограмата може да бъде извиквана чрез изписване
на името ѝ. +
Разгледайте примера:

[source,perl6]
----
sub alien-greeting {
  say "Hello earthlings";
}

alien-greeting;
----

В този пример е показана подпрограма, която не изисква никакви входни данни.

=== Списък с параметри
Много подпрограми изискват някакви входни данни, за да работят. Тези данни се
предоставят чрез подаване на *параметри*. Една подпрограма може да няма
никакви или да има няколко *параметъра*. Броят и типът на параметрите на една
подпрограма се наричат *сигнатура*.

 Следващата подпрограма приема низ като параметър.

[source,perl6]
----
sub say-hello (Str $name) {
    say "Hello " ~ $name ~ "!!!!"
}
say-hello "Paul";
say-hello "Paula";
----

=== Многократност
Може да създадете една подпрограма многократно (с едно и също име), но различен
списък от параметри.  Когато подпрограмата бъде извикана, средата за изпълнение
ще реши коя от тях да изпълни в зависимост от типа и броя на подадените
параметри.  Този тип подпрограми се създават както обикновено, но като
използвате ключовата дума `multi` вместо `sub`.

[source,perl6]
----
multi greet($name) {
    say "Good morning $name";
}
multi greet($name, $title) {
    say "Good morning $title $name";
}

greet "Johnnie";
greet "Laura","Mrs.";
----


NOTE: Оригиналното заглавие на секцията е "Multiple Dispatch". Множествено
разпределение или "много-методи" е свойство на някои програмни езици, при което
една функция или метод може да бъде динамично избрана за изпълнение в
зависимост от типа и броя на подадените ѝ аргументи. От Уикипедия:
https://en.wikipedia.org/wiki/Multiple_dispatch (Бел. Прев.)

=== Незадължителни и подразбиращи се параметри
Ако сте създали подпрограма, приемаща един параметър и я извикате, без да ѝ
подавате нищо, изпълнението ще се провали.

Освен това Пърл 6 ни дава възможност да създаваме подпрограми с:

* Незадължителни параметри
* Параметри със стойност по подразбиране

Незадължителните параметри се задават като в края на името на параметъра
(променливата) се добави `?`.

[source,perl6]
----
sub say-hello($name?) {
  with $name { say "Hello " ~ $name }
  else { say "Hello Human" }
}
say-hello;
say-hello("Laura");
----

Ако потребителят не подаде параметър, той може да приема определена стойност по подразбиране. +
Това се постига като присвоим стойността, когато създаваме подпрограмата.

[source,perl6]
----
sub say-hello($name="Matt") {
  say "Hello " ~ $name;
}
say-hello;
say-hello("Laura");
----

=== Връщане на стойности
Всички подпрограми дотук *правят нещо*, показват някакъв текст в терминала.

Въпреки, че това си е съвсем наред, понякога ни се иска подпрограмата да
*върне* някаква стойност, която можем да използваме по-късно в приложението.

По подразбиране, резултатът от изпълнението на последния ред в нашата
подпрограма е стойността, която тя връща.
[source,perl6]
.Подразбиращо се връщане на стойност
----
sub squared ($x) {
  $x ** 2;
}
say "7 squared is equal to " ~ squared(7);
----

И тъй като с времето количеството код в програмата ни нараства, е добра идея
_изрично_ да укажем какво връщаме.  Това се прави с ключовата дума `return`.
[source,perl6]
.Изрично връщане на стойност
----
sub squared ($x) {
  return $x ** 2;
}
say "7 squared is equal to " ~ squared(7);
----
==== Ограничаване на връщаните стойности
В един от предишните примери видяхме, че можем да зададем определен тип на
приемания параметър. Същото може да бъде направено с връщаната стойност.

За да ограничим типа на връщаната стойност, използуваме _отличителя_ `returns` или стрелка `-\->` в сигнатурата.

[source,perl6]
.Използуване на отличителя `returns`
----
sub squared ($x) returns Int {
  return $x ** 2;
}
say "1.2 на квадрат е " ~ squared(1.2);
----

[source,perl6]
.Използуване на стрелката
----
sub squared ($x --> Int) {
  return $x ** 2;
}
say "1.2 squared is equal to " ~ squared(1.2);
----
Ако не върнем стойност от същия тип, програмата ни ще хвърли грешка.

----
Type check failed for return value; expected Int but got Rat (1.44)
----

[TIP]
====
Ограниченията по тип могат да задават не само типа на връщаната стойност, но и
това, дали е дефинирана стойността.

В предишните примери указвахме, че типа на връщаната стойност трябва да бъде
`Int`, без да ни интересува дали стойността е дефинирана или не. Можехме и да
укажем изрично, дали върнатият `Int` трябва да бъде дефиниран (стойността
да не е `Nil`) или не. Това се постига с използуването на следните сигнатури: +
`--> Int:D` and `--> Int:U`

И така, да се използуват тези ограничения е добра практика. +
Ето долу новата версия на предния пример, като се използува `:D` за да се
ограничи връщаната стойност от тип `Int` да бъде дефинирана.

[source,perl6]
----
sub squared ($x --> Int:D) {
  return $x ** 2;
}
say "1.2 squared is equal to " ~ squared(1.2);
----
====

NOTE: За повече информация относно подпрограмите и функциите, вижте https://docs.perl6.org/language/functions

== Функционално Програмиране
В тази глава ще разгледаме част от възможностите на езика, които улесняват функционалното програмиране.

=== Функциите са граждани първа класа
Функциите/подпрограмите са граждани първа класа:

* Могат да се подават като параметър

* Могат да бъдат връщани от друга функция

* Могат да бъдат присвоявани като стойност на променливи

Прекрасен пример е функцията `map`. +
`map` е *функция от високо ниво*, тя приема друга функция като параметър.

[source,perl6]
.Скрипт
----
my @array = <1 2 3 4 5>;
sub squared($x) {
  $x ** 2
}
say map(&squared,@array);
----

.Изход
----
(1 4 9 16 25)
----

.Обяснение
Създадохме подпрограма, наречена `squared`. Тя ще повдигне на квадрат всяко подадено ѝ число. +
След това използвахме `map`, функция от високо ниво, и подадохме два параметъра - функция и масив. +
Изходът е списък от елементите на масива, повдигнати на квадрат.

Забележете, че когато подаваме функция като параметър, трябва да поставим пред името ѝ знака `&`.

=== Затваряния
Всички обекти, съставени от код в Пърл 6 са *затваряния* (closures). Това означава, че те
могат да се обръщат към лексикални (частни) променливи от заобикалящия ги блок във
времето на създаването им.
// Следния текст е преведен направо от документацията. Предложен и за английски
// https://github.com/hankache/perl6intro/issues/103
[source,perl6]
----
sub generate-sub($x) {
    my $y = 2 * $x;
    return sub { say $y };
    # inner sub, uses $y
}
my $generated = generate-sub(21);
$generated(); #42
----

Тук `$y` е частна променлива в `generate-sub` и вътрешната подпрограма, която
е върната, я използува. По времето, когато се извиква върнатата подпрограма,
`generate-sub` вече е завършила изпълнението си. Все пак върнатата подпрограма
може да използува променливата `$y`, защото я е затворила в себе си.

=== Безименни функции
*Безименната функция* се нарича още *ламбда*. +
Безименната функция не е обвързана с идентификатор (тя няма име).

Нека пренапишем примера с `map` като използваме безименна функция.
[source,perl6]
----
my @array = <1 2 3 4 5>;
say map(-> $x {$x ** 2},@array);
----
Забележете, че вместо да обявяваме подпрограма и да я подаваме като параметър на `map`, ние я създадохме направо в израза. +
Безименната подпрограма `\-> $x {$x ** 2}` няма "ръчка" (име) и не може да бъде извикана.

На жаргона на Пърл 6 наричаме това записване *остър блок*

[source,perl6]
.Остър блок може да се използува също за присвояване на функции на променливи:
----
my $squared = -> $x {
  $x ** 2
}
say $squared(9);
----

=== Верижност (Chaining)
В Пърл 6 методите могат да бъдат извикани верижно. Вече не се налага да
подавате изхода от един метод на друг.

NOTE: Всяка вградена функция може да се използува и като метод върху обект. (Бел. прев.)

Да разгледаме пример, в който ви е даден масив от стойности. От вас се изисква
да върнете неповтарящите се стойности, подредени в обратен ред.

Бихте могли да решите задачата като напишете нещо подобно на следното:
[source,perl6]
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array = reverse(sort(unique(@array)));
say @final-array;
----
Първо извикваме функцията `unique` като ѝ подаваме `@array`. След това подаваме
изхода от нея на `sort` и накрая подаваме изхода от подреждането на
`reverse`.

Независимо от примера горе, Пърл 6 позволява верижно извикване на методите. +
Можем да се възползваме от възможността за *верижно извикване* на методи и да
запишем примера по следния начин:

[source,perl6]
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array = @array.unique.sort.reverse;
say @final-array;
----

Вече виждате, че верижното извикване е _по-лесно за четене_.

=== Оператор за подаване
*Операторът за подаване*, наричан _тръба_ в някои езици за програмиране,
предоставя още по-добър начин за представяне на верижното извикване.
[source,perl6]
.Подаване напред
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
@array ==> unique()
       ==> sort()
       ==> reverse()
       ==> my @final-array;
say @final-array;
----

.Обяснение
----
Тръгваме от `@array`, след което връщаме списък с неповторими елементи
                    после ги подреждаме,
                    обръщаме реда им
                    и накрая съхраняваме изхода във @final-array
----
Виждате, че последователността на извикване на методите е отгоре надолу.

[source,perl6]
.Подаване назад
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array-v2 <== reverse()
                   <== sort()
                   <== unique()
                   <== @array;
say @final-array-v2;
----

.Обяснение
Подаването назад е като подаването напред, но наобратно. +
Последователността на извикване на методите е отдолу нагоре.

=== Свръх-оператор
*Свръх-операторът* (hyper operator) `>>.` ще извика даден метод върху всички елементи от едни
списък и ще върне списък с резултатите.
[source,perl6]
----
my @array = <0 1 2 3 4 5 6 7 8 9 10>;
sub is-even($var) { $var %% 2 };

say @array>>.is-prime;
say @array>>.&is-even;
----

Можем да използваме свръх-оператора и с вградените методи в Пърл 6, например
`is-prime`, който ни казва дали едно число е просто. +
Също така можем да създаваме нови подпрограми и да ги извикваме чрез
свръх-оператора. В този случай трябва да поставим `&` пред името на метода,
например `&is-even`.

Това е много практично, тъй като ни освобождава от писането на цикли `for`, за
да обхождаме всяка стойност от масива.

WARNING: Пърл 6 гарантира, че реда на изходите от работата на метода, извикан
чрез свръх-оператора, ще е същия като на входните стойности. +
Но *няма гаранция*, че Пърл 6 ще извика метода последователно, както е реда на
елементите, нито че извикването ще е в същата нишка. +
Така че, бъдете внимателни с методи, които имат странични ефекти като `say`
(където страничния ефект е показването на подадената стойност).

=== Сливания
*Сливането* (junction) е логическо съпоставяне на стойности.

В израза долу `1|2|3` е сливане.
[source,perl6]
----
my $var = 2;
if $var == 1|2|3 {
  say "Променливата има стойност 1 или 2 или 3"
}
----
Използуването на сливания предизвиква *автоматично създаване на нишки (autothreading)*;
операцията се извършва за всеки елемент от сливането, и всички резултати са събрани в ново сливане и върнати.
// Да предложа на автора да се каже повече за сливанията. Да се обясни 'и', не само 'или'.

=== Мързеливи списъци
*Мързелив списък* е този, който е изчислен мързеливо. +
Мързеливо означава отлагане на изчислението на даден израз до момента, когато е
необходимо и избягване повторение на изчислението, като се съхранява резултата
в паметта.

Ползите са:

* Нарастване на производителността чрез избягване на излишни изчисления;

* Възможността да се създават при необходимост безкрайни структури от данни;

* Възможността да се управлява изпълнението.

За да построим мързелив списък, използуваме вмъкнатия оператор `...`. +
Мързеливият списък има *начален(лни) елемент(и)*, *генератор* (начин за
създаване на списъка – бел. прев. ) и *край*.

[source,perl6]
.Примерен мързелив списък
----
my $lazylist = (1 ... 10);
say $lazylist;
----
Началният елемент е 1, а крайният е 10. Не е определен генератор за създаване
на списъка, така че се използува подразбиращият се генератор – последователно
нарастване с единица (+1) +
С други думи, този мързелив списък (ако е необходимо) ще върне следните
елементи (1, 2, 3, 4, 5, 6, 7, 8, 9, 10)

[source,perl6]
.Безкраен мързелив списък
----
my $lazylist = (1 ... Inf);
say $lazylist;
----
Този списък може да върне (ако е необходимо) всяко цяло число между 1 и
безкрайност, т.е. всяко цяло число.

[source,perl6]
.Мързелив списък, построен чрез изведен генератор
----
my $lazylist = (0,2 ... 10);
say $lazylist;
----
Първоначалните елементи са 0 и 2 а крайният елемент – 10.  Не е определен
генератор, но Пърл 6 ще изведе генератора от първоначалните елементи (+2).
Този мързелив списък може да върне (ако е необходимо) елементите (0, 2, 4, 6,
8, 10).

[source,perl6]
.Мързелив списък, построен чрез определен генератор.
----
my $мързел-списък = (0, { $_ + 3 } ... 12);
say $мързел-списък;
----
В този пример определихме изрично генератор, ограден с `{ }`. +
Този списък би върнал (ако е нужно) елементите (0, 3, 6, 9, 12).

[WARNING]
====
Ако използуваме изрично зададен генератор, крайният елемент трябва да е стойност,
която е възможно да бъде върната от генератора. +
Ако възпроизведем примера горе с краен елемент 10 вместо 12, генераторът няма да спре.
Генераторът ще _прескочи_ крайният елемент.

Може да заместите `0 ... 10` с `0 ...^ * > 10` +
Чете се по следния начин: От 0 до първата стойност по-голяма от 10 (но без нея).
[source,perl6]
.Това няма да спре генератора
----
my $lazylist = (0, { $_ + 3 } ... 10);
say $lazylist;
----

[source,perl6]
.Това ще спре генератора
----
my $lazylist = (0, { $_ + 3 } ...^ * > 10);
say $lazylist;
----
====

